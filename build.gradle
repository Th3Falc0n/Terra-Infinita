apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'eclipse'

if (System.getenv("TRAVIS_BUILD_NUMBER") != null) version = version + "." + System.getenv("TRAVIS_BUILD_NUMBER")

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "http://maven.lolhens.org/repository/internal"
    }
}

dependencies {
    compile([group: 'org.scala-lang', name: 'scala-library', version: version_scala],
            [group: 'com.typesafe.akka', name: 'akka-actor_2.11', version: version_akka],
            [group: 'com.badlogicgames.gdx', name: 'gdx', version: version_libgdx],
            [group: 'com.badlogicgames.gdx', name: 'gdx-freetype', version: version_libgdx],
            [group: 'com.badlogicgames.gdx', name: 'gdx-backend-lwjgl', version: version_libgdx],
            [group: 'com.badlogicgames.gdx', name: 'gdx-platform', version: version_libgdx, classifier: 'natives-desktop'],
            [group: 'com.badlogicgames.gdx', name: 'gdx-freetype-platform', version: version_libgdx, classifier: 'natives-desktop'],
            [group: 'org.lolhens', name: 'LibEventManager', version: version_eventManager])
}

jar {
    manifest {
        if (project.hasProperty("mainClass")) attributes 'Main-Class': mainClass
    }
}

task testJar(type: Jar, dependsOn: javadoc) {
    from sourceSets.main.output
    from sourceSets.test.output

    manifest {
        if (project.hasProperty("testMainClass")) attributes "Main-Class": testMainClass
    }
    classifier = 'test'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier = 'javadoc'
}

build {
    dependsOn testJar
    dependsOn sourceJar
    dependsOn javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact testJar
            artifact sourceJar
            artifact javadocJar
        }
    }

    repositories.maven {
        if (!project.hasProperty("mavenUrl")) return;

        credentials {
            username System.getenv("CI_DEPLOY_USERNAME")
            password System.getenv("CI_DEPLOY_PASSWORD")
        }
        url mavenUrl
    }
}